import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript { handler ->
  apply from: file('gradle/repositories.gradle'), to: handler

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.0-alpha06'
    classpath 'com.google.gms:google-services:4.2.0'
    classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.21'
  }
}

apply from: file('gradle/git-release-notes.gradle')
apply from: file('gradle/gradle-mvn-push.gradle')
apply from: file('gradle/repositories.gradle')

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 28

  defaultConfig {
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
    }

    minSdkVersion 14
    targetSdkVersion 28
  }

  sourceSets {
    main.java.srcDirs 'src/main/kotlin'
    test.java.srcDirs 'src/test/kotlin'
  }
}

dependencies {
  implementation 'com.google.android.gms:play-services-tasks:16.0.1'
  implementation 'com.google.firebase:firebase-auth:16.1.0'
  implementation 'com.google.firebase:firebase-core:16.0.7'
  implementation 'com.google.firebase:firebase-database:16.1.0'
  implementation 'io.ashdavies.rx:rx-tasks:2.2.0'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.21'

  testImplementation 'com.google.truth:truth:0.34'
  testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.4.0'
  testImplementation 'org.mockito:mockito-android:2.24.5' because '2.24.6 not available'
}

tasks
    .withType(Javadoc)
    .all { enabled = false }

tasks
    .withType(KotlinCompile)
    .all { kotlinOptions.freeCompilerArgs += ["-XXLanguage:+InlineClasses"] }
